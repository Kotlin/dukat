import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

plugins {
    id("kotlin")
    id("com.moowork.node")
}

configurations {
    toDownload
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation(project(":ast-common"))
    implementation(project(":ast-declarations"))
    implementation(project(":ast-model"))
    implementation(project(":idl-declarations"))
    implementation(project(":idl-models"))
    implementation(project(":idl-parser"))
    implementation(project(":idl-reference-resolver"))
    implementation(project(":itertools"))
    implementation(project(":logging"))
    implementation(project(":model-lowerings"))
    implementation(project(":module-name-resolver"))
    implementation(project(":node-introduction"))
    implementation(project(":node-lowering"))
    implementation(project(":ownerContext"))
    implementation(project(":panic"))
    implementation(project(":translator"))
    implementation(project(":translator-string"))
    implementation(project(":ts-translator"))
    implementation(project(":tsmodel"))
    implementation(project(":idl-lowerings"))
    implementation(project(":ts-lowerings"))

    runtime 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // without this dependency one won't see "Click to see difference" in IDEA
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    testImplementation "org.junit.jupiter:junit-jupiter-params:${gradle.jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${gradle.jupiterVersion}"

    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin"
    testImplementation "org.jetbrains.kotlin:kotlin-compiler-embeddable"

    compile(project(path: ':ts', configuration: 'dukatTsResources'))

    toDownload 'org.jetbrains.kotlin:kotlin-stdlib-js'
}

def getTsDependency() {
    return zipTree(project(":ts").getTasksByName("createJar", true).archivePath[0])
}

processResources {
    dependsOn = [":ts:createJar"]
    from(getTsDependency()) {
        into "/"
    }
}

task propagateToIdeaRuntime(type: Copy) {
    dependsOn = [":ts:createJar"]

    from(getTsDependency())
    into "out/production/resources/"
}

compileTestKotlin.dependsOn = ["propagateToIdeaRuntime"]

task download(type: Copy) {
    from configurations.toDownload
    into "${project.buildDir}/lib"
    rename "kotlin-stdlib-js-${gradle.kotlinVersion}.jar", "kotlin-stdlib-js.jar"
}


task downloadDefinitelyTyped(type: Download) {
    onlyIfModified true
    overwrite false
    def version = '10aca630788ef193f994ac31de63ea9cf44630a3'
    src "https://github.com/DefinitelyTyped/DefinitelyTyped/archive/${version}.zip"
    dest new File(rootProject.gradle.gradleUserHomeDir, "DefinitelyTyped-${version}.zip")
}

task fetchDefinitelyTyped(dependsOn: downloadDefinitelyTyped, type: Copy) {
    from zipTree(downloadDefinitelyTyped.dest)
    into "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped"
}

test.dependsOn(download)

task generateBinary(type: NodeTask) {
    def scriptName = "${project(":ts").buildDir}/bundle/runtime.js"
    script = file(scriptName)

    def declarationsDir = "${project.projectDir}/test/data/typescript"
    def libPath = "${project(":ts").buildDir}/package/node_modules/typescript/lib/lib.d.ts"

    inputs.dir(declarationsDir)
    inputs.file(scriptName)
    inputs.file(libPath)

    def outputFile = "${project.buildDir}/declarations.dukat"
    outputs.file(outputFile)

    def files = []
    file(declarationsDir)
            .eachFileRecurse() {
                if (it.isFile() && it.name.endsWith(".d.ts")) {
                    files << it
                }
            }

    args = [
            libPath,
            outputFile
    ] + files
}

generateBinary.dependsOn = [":ts:webpack"]

test.dependsOn = [generateBinary]

test {
    [
            "dukat.test.extended",
            "dukat.test.failure.always"
    ].forEach { String projectProperty ->
        if (project.hasProperty(projectProperty)) {
            systemProperty(projectProperty, "true")
        }
    }

    systemProperty("dukat.test.resources.definitelyTyped", "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped")
}