import de.undercouch.gradle.tasks.download.Download
import static groovy.io.FileType.FILES

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

plugins {
    id("kotlin")
    id("com.moowork.node")
    id("kotlinx-serialization")
}

configurations {
    toDownload
    kotlinJsLibs
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

def DEF_TYPE_CHANGESET = '31929c09c7b4490f87766206b412da4a6a581dc3'
def DEF_TYPE_DIR = "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped/DefinitelyTyped-${DEF_TYPE_CHANGESET}/types"

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation(project(":ast-common"))
    implementation(project(":ts-ast-declarations"))
    implementation(project(":ast-model"))
    implementation(project(":idl-declarations"))
    implementation(project(":idl-models"))
    implementation(project(":idl-parser"))
    implementation(project(":idl-reference-resolver"))
    implementation(project(":itertools"))
    implementation(project(":logging"))
    implementation(project(":model-lowerings"))
    implementation(project(":module-name-resolver"))
    implementation(project(":ts-node-introduction"))
    implementation(project(":ts-node-lowering"))
    implementation(project(":ownerContext"))
    implementation(project(":panic"))
    implementation(project(":translator"))
    implementation(project(":translator-string"))
    implementation(project(":ts-translator"))
    implementation(project(":ts-model"))
    implementation(project(":idl-lowerings"))
    implementation(project(":ts-lowerings"))

    runtime 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // without this dependency one won't see "Click to see difference" in IDEA
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    testImplementation "org.junit.jupiter:junit-jupiter-params:${gradle.jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${gradle.jupiterVersion}"

    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin"
    testImplementation "org.jetbrains.kotlin:kotlin-compiler-embeddable"

    testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${gradle.kotlinSerializationRuntimeVersion}"

    compile(project(path: ":ts-converter", configuration: 'dukatTsResources'))

    kotlinJsLibs("org.jetbrains.kotlin:kotlin-stdlib-js:${gradle.kotlinVersion}")

    toDownload 'org.jetbrains.kotlin:kotlin-stdlib-js'
}

def getTsDependency() {
    return zipTree(project(":ts-converter").getTasksByName("createJar", true).archivePath[0])
}

processResources {
    dependsOn = [":ts-converter:createJar"]
    from(getTsDependency()) {
        into "/"
    }
}

task propagateToIdeaRuntime(type: Copy) {
    dependsOn = [":ts-converter:createJar"]

    from(getTsDependency())
    into "out/production/resources/"
}

compileTestKotlin.dependsOn = ["propagateToIdeaRuntime"]

task download(type: Copy) {
    from configurations.toDownload
    into "${project.buildDir}/lib"
    rename "kotlin-stdlib-js-${gradle.kotlinVersion}.jar", "kotlin-stdlib-js.jar"
}


task downloadDefinitelyTyped(type: Download) {
    onlyIfModified true
    overwrite false
    src "https://github.com/DefinitelyTyped/DefinitelyTyped/archive/${DEF_TYPE_CHANGESET}.zip"
    dest new File(rootProject.gradle.gradleUserHomeDir, "DefinitelyTyped-${DEF_TYPE_CHANGESET}.zip")
}

task fetchDefinitelyTyped(dependsOn: downloadDefinitelyTyped, type: Copy) {
    from zipTree(downloadDefinitelyTyped.dest)
    into "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped"
}

test.dependsOn(download)

task generateBinary(type: NodeTask) {
    def scriptName = "${project(":ts-converter").buildDir}/bundle/runtime.js"
    script = file(scriptName)

    def declarationsDir = "${project.projectDir}/test/data/typescript"
    def libPath = "${project(":ts-converter").buildDir}/package/node_modules/typescript/lib/lib.d.ts"

    inputs.dir(declarationsDir)
    inputs.file(scriptName)
    inputs.file(libPath)

    def outputFile = "${project.buildDir}/declarations.dukat"
    outputs.file(outputFile)

    def files = []
    file(declarationsDir)
            .eachFileRecurse() {
                if (it.isFile() && it.name.endsWith(".d.ts")) {
                    files << it
                }
            }

    args = [
            libPath,
            outputFile
    ] + files
}

task generateBatchConfig() {
    dependsOn = [fetchDefinitelyTyped]

    def batchFile = file("${project.buildDir}/batch.list")
    inputs.dir(DEF_TYPE_DIR)
    outputs.file(batchFile)

    doLast {
        batchFile.text = ""
        def newLine = System.getProperty("line.separator")

        file(DEF_TYPE_DIR).eachFileRecurse (FILES) { file ->
            if (file.name == "index.d.ts") {
                batchFile.append("-d ${project.buildDir}/tests/definitelyTyped/${file.parentFile.name} ${file.absolutePath}${newLine}")
            }
        }
    }
}

task extractKoltinJsLibs(type: Copy) {
    configurations.kotlinJsLibs.each {
        from it.absolutePath
    }
    into "${project.buildDir}/kotlinHome"
    rename { String fileName ->
        return fileName.replace("-${gradle.kotlinVersion}.jar", ".jar")
    }
}

task generateBatch(type: NodeTask) {
    dependsOn = [generateBatchConfig, ":node-package:buildDistrib"]

    inputs.file("${project.buildDir}/batch.list")
    outputs.dir("${project.buildDir}/tests/definitelyTyped")

    script = file("${project(":node-package").buildDir}/distrib/bin/dukat-cli.js")
    args = ["--batch", "${project.buildDir}/batch.list"]
}

generateBinary.dependsOn = [":ts-converter:webpack"]

test.dependsOn = [generateBinary, fetchDefinitelyTyped, extractKoltinJsLibs, ":node-package:nodeEnv", ":node-package:buildDistrib"]

test {
    [
            "dukat.test.extended",
            "dukat.test.failure.always"
    ].forEach { String projectProperty ->
        if (project.hasProperty(projectProperty)) {
            systemProperty(projectProperty, "true")
        }
    }

    systemProperty("dukat.test.resources.definitelyTyped", DEF_TYPE_DIR)

    testLogging {
        showStandardStreams = true
    }
}