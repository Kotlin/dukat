plugins {
    id("base")
    id("com.moowork.node")
}

def PROPERTY_PACKAGE_VERSION = "dukat.npm.version"
def PROPERTY_REGISTRY = "dukat.npm.registry"
def DISTRIB_PATH = "${project.buildDir}/distrib"
def typescriptVersion = hasProperty("tsVersion") ? tsVersion : gradle.defaultTsVersion

node {
    workDir = gradle.nodeWorkingDir

    version = gradle.nodeVersion
    npmVersion = gradle.npmVersion
    yarnVersion = gradle.yarnVersion

    nodeModulesDir = file(DISTRIB_PATH)
    download = true
}

configurations {
    cliBinary
}

task prepareNpmPackage {
    doLast {
        copy {
            from "package.template.json"
            into DISTRIB_PATH
            filter { String line -> line.replaceAll("__TS_VERSION__", typescriptVersion) }
            rename { String fileName -> fileName.replace(".template", "") }
        }

        copy {
            from "yarn.lock"
            into DISTRIB_PATH
        }
    }
}

yarn.dependsOn = [yarnSetup, prepareNpmPackage]

task buildDistrib {
    dependsOn = [":ts-converter:webpack", yarn, ":command-line:build"]

    doLast {
        copy {
            from "${rootProject.projectDir}/README.md"
            into DISTRIB_PATH
        }

        copy {
            from "${project(":ts-converter").buildDir}/package/node_modules/typescript/lib"
            include "*.d.ts"
            into "${DISTRIB_PATH}/d.ts.libs"
        }

        copy {
            from "${project(":ts-converter").buildDir}/bundle/converter.js"
            into "${DISTRIB_PATH}/lib"
        }

        copy {
            from "${project(":ts-converter").buildDir}/bundle/runtime.js"
            into "${DISTRIB_PATH}/lib"
        }

        copy {
            from "bin"
            into "${DISTRIB_PATH}/bin"
        }

        copy {
            from configurations.cliBinary
            into "${DISTRIB_PATH}/build/runtime"
        }

    }
}

task npmVersion(type: NpmTask) {
    onlyIf {
        project.hasProperty(PROPERTY_PACKAGE_VERSION)
    }
    args = ["version", project.findProperty(PROPERTY_PACKAGE_VERSION) ?: "NEVER" ]
}

task nodeEnv(type: NodeTask) {
    doFirst {
        mkdir project.buildDir
    }
    script = file("env.js")
    args = ["${project.buildDir}/env.json"]
}


task npmPack(type: NpmTask) {
    dependsOn = [":command-line:build", buildDistrib, npmVersion]
    args = ["pack"]
}

task npmrc() {
    doLast {
        def token = getProperty("kotlin.npmjs.auth.token")
        def registry = project.findProperty(PROPERTY_REGISTRY) ?: "registry.npmjs.org"
        def customConfig = """
//${registry}/:_authToken="${token}"
        """
        def configFile = "${DISTRIB_PATH}/.npmrc"
        file(configFile).text = customConfig;
    }
}

task npmPublish(type: NpmTask) {
    dependsOn = [npmrc, npmPack]

    def packageVersion =
            project.findProperty(PROPERTY_PACKAGE_VERSION) ?:
            new groovy.json.JsonSlurper().parseText(file("package.template.json").text).version

    def distribPath = "dukat-${packageVersion}.tgz"

    def registry = project.findProperty(PROPERTY_REGISTRY) ?: "registry.npmjs.org"
    args = ["publish",  distribPath,  "--registry", "http://${registry}"]
}

dependencies {
    cliBinary(project(":command-line")) {
        transitive = false
    }
}