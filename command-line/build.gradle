import groovy.json.JsonSlurper

plugins {
  id("kotlin")
  id("kotlinx-serialization")
  id("maven-publish")
  id "com.jfrog.bintray" version "1.8.4"
}

configurations {
  cliRuntime
}


dependencies {
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${gradle.kotlinSerializationRuntimeVersion}"

  implementation(project(":ast-common"))
  implementation(project(":ast-model"))
  implementation(project(":compiler"))
  implementation(project(":descriptors"))
  implementation(project(":idl-reference-resolver"))
  implementation(project(":js-translator"))
  implementation(project(":module-name-resolver"))
  implementation(project(":panic"))
  implementation(project(":translator"))
  implementation(project(":translator-string"))
  implementation(project(":ts-ast-declarations"))
  implementation(project(":ts-model"))
  implementation(project(":ts-translator"))


  cliRuntime(project(":compiler"))
}

jar {
  archiveName = "dukat-cli.jar"
  manifest {
    attributes 'Main-Class': 'org.jetbrains.dukat.cli.CliKt'
  }


  from {
    configurations.cliRuntime.collect { it.isDirectory() ? it : zipTree(it) }
  }

}

def configFile = file("${project(":node-package").projectDir}/package.template.json")
def json = new JsonSlurper().parseText(configFile.text)


// TODO: Duplication of logic in node-package/build.gradle
def PROPERTY_PACKAGE = "dukat.npm.packageVersion"
def PACKAGE_VERSION = hasProperty(PROPERTY_PACKAGE) ? getProperty(PROPERTY_PACKAGE)
        .replace("__DEFAULT__", gradle.defaultNpmPackageVersion)
        .replace("__TIMESTAMP__", new Date().format("yyyyMMdd")) : gradle.defaultNpmPackageVersion

def dukatVersion = json.version == "__PACKAGE_VERSION__" ? PACKAGE_VERSION : json.version

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_API_KEY')
  publications = ['mavenJava']
  publish = true
  pkg {
    repo = 'dukat'
    name = 'dukat'
    userOrg = 'kotlin'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/kotlin/dukat.git'
    version {
      name = dukatVersion
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId "org.jetbrains.dukat"
      artifactId "dukat"
      version dukatVersion
      artifact jar
    }
  }
}
