import groovy.json.JsonSlurper

plugins {
  id("kotlin")
  id("kotlinx-serialization")
  id("maven-publish")
}

configurations {
  cliRuntime
}


dependencies {
  implementation("org.jetbrains.kotlinx:kotlinx-serialization-core:${gradle.kotlinSerializationVersion}")

  implementation(project(":ast-common"))
  implementation(project(":ast-model"))
  implementation(project(path: ":compiler", configuration: "default"))
  implementation(project(":descriptors"))
  implementation(project(":idl-reference-resolver"))
  implementation(project(":js-translator"))
  implementation(project(":module-name-resolver"))
  implementation(project(":panic"))
  implementation(project(":translator"))
  implementation(project(":translator-string"))
  implementation(project(":ts-ast-declarations"))
  implementation(project(":ts-model"))
  implementation(project(":ts-translator"))

  cliRuntime(project(":compiler"))
}

jar {
  archiveName = "dukat-cli.jar"
  manifest {
    attributes 'Main-Class': 'org.jetbrains.dukat.cli.CliKt'
  }


  from {
    configurations.cliRuntime.collect { it.isDirectory() ? it : zipTree(it) }
  }

}

def configFile = file("${project(":node-package").projectDir}/package.template.json")
def json = new JsonSlurper().parseText(configFile.text)


// TODO: Duplication of logic in node-package/build.gradle
def PROPERTY_PACKAGE = "dukat.npm.packageVersion"
def PACKAGE_VERSION = hasProperty(PROPERTY_PACKAGE) ? getProperty(PROPERTY_PACKAGE)
        .replace("__DEFAULT__", gradle.defaultNpmPackageVersion)
        .replace("__TIMESTAMP__", new Date().format("yyyyMMdd")) : gradle.defaultNpmPackageVersion

def dukatVersion = json.version == "__PACKAGE_VERSION__" ? PACKAGE_VERSION : json.version

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId "org.jetbrains.dukat"
      artifactId "dukat"
      version dukatVersion
      artifact jar
    }
  }
  repositories {
    maven {
      name = "kotlinSpace"
      url = uri "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-dependencies"
      credentials PasswordCredentials
    }
  }
}
