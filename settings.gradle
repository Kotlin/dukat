pluginManagement {
  repositories {
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
  }

  resolutionStrategy {
    def kotlin_plugins = ["kotlin2js", "kotlin", "kotlin-multiplatform"]

    eachPlugin {
      def pluginId = requested.id.id
      if (pluginId in kotlin_plugins) {
        useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${gradle.kotlinVersion}")
      } else if (pluginId == "kotlinx-serialization"){
        useModule("org.jetbrains.kotlin:kotlin-serialization:${gradle.kotlinVersion}")
      } else if (pluginId == 'com.google.protobuf') {
        useModule("com.google.protobuf:protobuf-gradle-plugin:${gradle.protobufGradlePluginVersion}")
      } else  if (pluginId == "com.moowork.node") {
        useModule("com.moowork.gradle:gradle-node-plugin:${gradle.nodePluginVersion}")
      }
    }
  }
}

gradle.ext {
  kotlinVersion = "1.3.50"
  jupiterVersion = "5.4.1"
  nodeVersion = "10.16.2"
  npmVersion = "6.9.0"
  nodePluginVersion = "1.3.1"
  kotlinSerializationRuntimeVersion = "0.11.0"
  antlr4_version = "4.7.1"
  protobufGradlePluginVersion = "0.8.10"
  protobufImplementationVersion = "3.9.0"
}


include 'ast-declarations'
include 'ast-common'
include 'ast-model'
include 'cli'
include 'compiler'
include 'model-lowerings'
include 'idl-declarations'
include 'idl-lowerings'
include 'idl-models'
include 'idl-parser'
include 'idl-reference-resolver'
include 'itertools'
include 'interop'
include 'logging'
include 'module-name-resolver'
include 'node-introduction'
include 'node-package'
include 'node-lowering'
include 'ownerContext'
include 'panic'
include 'translator-string'
include 'ts-converter'
include 'tsmodel'
include 'tsmodel-proto'
include 'translator'
include 'ts-lowerings'
include 'ts-translator'

project(':idl-declarations').projectDir = file("$rootDir/idl/idl-declarations")
project(':idl-lowerings').projectDir = file("$rootDir/idl/idl-lowerings")
project(':idl-models').projectDir = file("$rootDir/idl/idl-models")
project(':idl-parser').projectDir = file("$rootDir/idl/idl-parser")
project(':idl-reference-resolver').projectDir = file("$rootDir/idl/idl-reference-resolver")
project(':tsmodel-proto').projectDir = file("$rootDir/typescript/tsmodel-proto")

project(':ts-lowerings').projectDir = file("$rootDir/typescript/ts-lowerings")
project(':ts-translator').projectDir = file("$rootDir/typescript/ts-translator")
project(':ast-declarations').projectDir = file("$rootDir/typescript/ast-declarations")
project(':ts-converter').projectDir = file("$rootDir/typescript/converter")

