pluginManagement {
  repositories {
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
      url "https://cache-redirector.jetbrains.com/jcenter.bintray.com"
    }
  }

  resolutionStrategy {
    def kotlin_plugins = ["kotlin2js", "kotlin", "kotlin-multiplatform"]

    eachPlugin {
      def pluginId = requested.id.id
      if (pluginId in kotlin_plugins) {
        useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${gradle.kotlinVersion}")
      } else if (pluginId == "kotlinx-serialization") {
        useModule("org.jetbrains.kotlin:kotlin-serialization:${gradle.kotlinVersion}")
      } else if (pluginId == 'com.google.protobuf') {
        useModule("com.google.protobuf:protobuf-gradle-plugin:${gradle.protobufGradlePluginVersion}")
      } else if (pluginId == "com.moowork.node") {
        useModule("com.moowork.gradle:gradle-node-plugin:${gradle.nodePluginVersion}")
      }
    }
  }
}

def buildLog(message) {
  println("[BUILD] ${message}")
}

gradle.ext {
  try {
    //TODO: for some reason hasProperty is not working for me
    kotlinVersion = getProperty("dukatKotlinVersion")
  } catch (e) {
    kotlinVersion = "1.3.70"
  }

  jupiterVersion = "5.6.0"
  nodeWorkingDir = file("${gradle.gradleUserHomeDir}/nodejs")
  nodeVersion = "13.7.0"
  npmVersion = "6.13.4"
  yarnVersion = "1.17.3"
  nodePluginVersion = "1.3.1"
  defaultTsVersion = "3.5.3"
  kotlinSerializationRuntimeVersion = "0.20.0"
  antlr4_version = "4.7.1"
  protobufGradlePluginVersion = "0.8.10"
  protobufImplementationVersion = "3.11.1"
}
buildLog("gradle settings properties: ${gradle.ext.properties}")

include 'ast-common'
include 'ast-model'
include 'command-line'
include 'graphs'
include 'model-lowerings-common'
include 'compiler'
include 'descriptors'
include 'descriptors-1.3.60'
include 'descriptors-1.3.70'
include 'idl-declarations'
include 'idl-lowerings'
include 'idl-models'
include 'idl-parser'
include 'idl-reference-resolver'
include 'itertools'
include 'js-module-statistics'
include 'js-translator'
include 'js-type-analysis'
include 'logging'
include 'model-lowerings'
include 'module-name-resolver'
include 'node-package'
include 'ownerContext'
include 'panic'
include 'stdlib'
include 'translator'
include 'translator-string'
include 'ts-ast-declarations'
include 'ts-converter'
include 'ts-lowerings'
include 'ts-model'
include 'ts-model-proto'
include 'ts-node-introduction'
include 'ts-node-lowering'
include 'ts-translator'

project(':descriptors-1.3.60').projectDir = file("$rootDir/version-specific/descriptors-1.3.60")
project(':descriptors-1.3.70').projectDir = file("$rootDir/version-specific/descriptors-1.3.70")

project(':graphs').projectDir = file("$rootDir/abstract/graphs")
project(':itertools').projectDir = file("$rootDir/abstract/itertools")

project(':idl-declarations').projectDir = file("$rootDir/idl/idl-declarations")
project(':idl-lowerings').projectDir = file("$rootDir/idl/idl-lowerings")
project(':idl-models').projectDir = file("$rootDir/idl/idl-models")
project(':idl-parser').projectDir = file("$rootDir/idl/idl-parser")
project(':idl-reference-resolver').projectDir = file("$rootDir/idl/idl-reference-resolver")

project(':js-module-statistics').projectDir = file("$rootDir/javascript/js-module-statistics")
project(':js-translator').projectDir = file("$rootDir/javascript/js-translator")
project(':js-type-analysis').projectDir = file("$rootDir/javascript/js-type-analysis")

project(':ts-ast-declarations').projectDir = file("$rootDir/typescript/ts-ast-declarations")
project(':ts-converter').projectDir = file("$rootDir/typescript/ts-converter")
project(':ts-lowerings').projectDir = file("$rootDir/typescript/ts-lowerings")
project(':ts-model').projectDir = file("$rootDir/typescript/ts-model")
project(':ts-model-proto').projectDir = file("$rootDir/typescript/ts-model-proto")
project(':ts-node-introduction').projectDir = file("$rootDir/typescript/ts-node-introduction")
project(':ts-node-lowering').projectDir = file("$rootDir/typescript/ts-node-lowering")
project(':ts-translator').projectDir = file("$rootDir/typescript/ts-translator")

