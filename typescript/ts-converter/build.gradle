plugins {
  id "java"
  id "com.moowork.node"
}

def packagePath = "${project.buildDir}/package"
def typescriptVersion = hasProperty("tsVersion") ? tsVersion : gradle.defaultTsVersion
def tsDeclarationsPath = ".tsdeclarations"

node {
  workDir = gradle.nodeWorkingDir

  // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
  version = gradle.nodeVersion
  yarnVersion = gradle.yarnVersion

  download = true

  nodeModulesDir = file(packagePath)
}

task prepareNpmPackage(type: Copy) {
  from "package.template.json"
  into packagePath
  filter{ String line -> line.replaceAll("__TS_VERSION__", typescriptVersion) }
  rename { String fileName -> fileName.replace(".template", "") }

  from "yarn.lock"
  into packagePath
}

yarn.dependsOn = [yarnSetup, prepareNpmPackage]

task copyTypescriptDeclarations(type: Copy) {
  dependsOn = ["yarn"]

  from "${packagePath}/node_modules/typescript/lib"
  into tsDeclarationsPath
  include "*.d.ts"
}

task compileTs(type: NodeTask) {
  dependsOn = [yarn, copyTypescriptDeclarations, ":ts-model-proto:build"]
  inputs.file("./tsconfig.json")
  inputs.dir("./src")
  inputs.dir(packagePath)
  outputs.dir("./build/ts")

  script = file("${packagePath}/node_modules/typescript/bin/tsc")

  args = ["-p", "tsconfig.json"]
}

task webpack(type: NodeTask) {
  dependsOn = ["compileTs", ":ts-model-proto:build"]
  def scriptName = "${project.buildDir}/package/node_modules/webpack/bin/webpack.js"

  inputs.file(scriptName)
  inputs.dir("${project(":ts-model-proto").buildDir}/generated/source/proto/main/js")
  inputs.dir("${project.buildDir}/ts")
  inputs.dir("${project.buildDir}/package/node_modules")

  outputs.file("${project.buildDir}/bundle/converter.js")
  outputs.file("${project.buildDir}/bundle/runtime.js")

  script = file(scriptName)
}

task createJar(type: Zip) {
  dependsOn = [webpack]
  baseName 'dukatts'
  extension 'jar'
  destinationDir file("${project.buildDir}/jar")

  from("${packagePath}/node_modules/typescript/lib/tsserverlibrary.js") {
    into 'ts'
  }

  from("${packagePath}/node_modules/typescript/lib") {
    include "**/*.d.ts"
    into 'ts'
  }

  from('build/bundle/converter.js') {
    into 'js'
  }

}

configurations {
  dukatTsResources
}

configurations.default.extendsFrom(configurations.dukatTsResources)

clean {
  delete tsDeclarationsPath
}